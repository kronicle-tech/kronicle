plugins {
    id "java-library"
    id "io.freefair.lombok"
}

dependencies {
    annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    compileOnly platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    testAnnotationProcessor platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    testImplementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")

    compileOnly "com.github.spotbugs:spotbugs-annotations:$spotBugsVersion"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:$spotBugsVersion"
    compileOnly "javax.inject:javax.inject:$javaxInjectApiVersion"
    testCompileOnly "javax.inject:javax.inject:$javaxInjectApiVersion"


    api project(":common")
    api project(":metadata")
    api "org.hibernate.validator:hibernate-validator"
    api("org.pf4j:pf4j:$pf4jVersion") {
        exclude group: 'org.apache.logging.log4j'
    }
    api ("org.pf4j:pf4j-spring:$pf4jSpringVersion") {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }
    api "org.springframework:spring-context"
    api "jakarta.annotation:jakarta.annotation-api"
    api "jakarta.validation:jakarta.validation-api"
    implementation "org.springframework:spring-webflux"

    testImplementation project(":service-core-tests")
    testImplementation "org.springframework.boot:spring-boot-starter-test", {
        exclude group: "junit", module: "junit"
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testImplementation "com.github.tomakehurst:wiremock-jre8:$wireMockVersion"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    maxParallelForks = Math.min(2, Runtime.runtime.availableProcessors().intdiv(2) ?: 1)
}
