import org.siouan.frontendgradleplugin.infrastructure.gradle.RunNpx

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "cz.habarta.typescript-generator:typescript-generator-gradle-plugin:$typescriptGeneratorPluginVersion"
  }
}

plugins {
  id "org.siouan.frontend-jdk11"
  id "com.palantir.docker"
}

apply plugin: "cz.habarta.typescript-generator"

frontend {
  nodeDistributionProvided = true
  assembleScript = "run build"
  cleanScript = "run clean"
  checkScript = "run check"
}

// TODO: According to the plugin's documentation, this should not be needed
cleanFrontend.dependsOn installFrontend
assembleFrontend.dependsOn installFrontend
checkFrontend.dependsOn assembleFrontend

tasks.register('e2e', RunNpx) {
  dependsOn tasks.named('installNode')
  script = 'cypress run'
}

dependencies {
  implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
  implementation platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion")

  implementation project(":sdk")
  implementation "org.springframework:spring-core"
}

generateTypeScript {
  jsonLibrary = "jackson2"
  classPatterns = ["tech.kronicle.sdk.models.**"]
  optionalAnnotations = ["org.springframework.lang.Nullable"]
  mapDate = "asString"
  outputFile = "types/kronicle-service/index.d.ts"
  outputKind = "module"
}

assembleFrontend.dependsOn generateTypeScript

docker {
  name "kronicle-app:$version$dockerTagSuffix"

  if (project.dockerPushImage.toBoolean()) {
    name = "$dockerRegistry/$name"
  }

  buildArgs([VERSION: version])
  tag "latest", name
  copySpec.from(".") {
    exclude ".nuxt"
    exclude "build"
    exclude "build.gradle"
    exclude "coverage"
    exclude "Dockerfile"
    exclude "node_modules"
    exclude "README.md"
  }
  network "host"
}

tasks.dockerPrepare.dependsOn installFrontend
tasks.dockerPrepare.dependsOn generateTypeScript
build.dependsOn tasks.docker
