plugins {
    id "java-library"
}

group "tech.kronicle"
archivesBaseName = "kronicle-$name"
description "Kronicle metadata library used for verifying that the contents of a Kronicle metdata YAML file is valid"

apply from: "$rootDir/gradle/publish.gradle"

dependencies {
    ["org.springframework.boot:spring-boot-dependencies:$springBootVersion",
     "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"].each {
        annotationProcessor platform(it)
        implementation platform(it)
        compileOnly platform(it)
        testAnnotationProcessor platform(it)
        testImplementation platform(it)
    }

    annotationProcessor "org.projectlombok:lombok"
    compileOnly "org.projectlombok:lombok"
    compileOnly "com.github.spotbugs:spotbugs-annotations:$spotBugsVersion"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    api project(':sdk')
    implementation project(':common')
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    implementation "org.hibernate.validator:hibernate-validator"
    implementation "org.glassfish:javax.el:$expressionLanguageVersion"

    testAnnotationProcessor "org.projectlombok:lombok"
    testCompileOnly "org.projectlombok:lombok"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:$spotBugsVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.mockito:mockito-junit-jupiter"
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}
